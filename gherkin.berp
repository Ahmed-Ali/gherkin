[
	Tokens -> #Empty,#Comment,#TagLine,#FeatureLine,#BackgroundLine,#ScenarioLine,#ScenarioOutlineLine,#ExamplesLine,#StepLine,#DocStringSeparator,#DocStringAlternativeSeparator,#TableRow
	IgnoredTokens -> #Comment,#Empty
	ClassName -> Parser
	Namespace -> Gherkin
]


Feature_File! := Feature_Header Background? Scenario_Base*
Feature_Header! := Tags? #FeatureLine Feature_Description

Background! := #BackgroundLine Background_Description Scenario_Step*

// we could avoid defining Scenario_Base, but that would require regular look-aheads, so worse performance
Scenario_Base! := Tags? (Scenario | ScenarioOutline)

Scenario! := #ScenarioLine Scenario_Description Scenario_Step*

ScenarioOutline! := #ScenarioOutlineLine ScenarioOutline_Description ScenarioOutline_Step* Examples+
// after the first "Examples" block, interpreting a tag line is ambiguous (tagline of next examples or of next scenario)
// because of this, we need a lookahead hint, that connects the tag line to the next examples, if there is an examples block ahead
Examples! [#Empty|#Comment|#TagLine->#ExamplesLine]:= Tags? #ExamplesLine Examples_Description Examples_Table
Examples_Table := #TableRow+

Scenario_Step := Step
ScenarioOutline_Step := Step

Step! := #StepLine Step_Arg?
Step_Arg := (DataTable | DocString)

DataTable! := #TableRow+
// supporting the two separators could be handled in lexer too, but this way it is easier
DocString! := (DocString_QuoteSepatator | DocString_AlternativeSepatator)
DocString_QuoteSepatator := #DocStringSeparator (#Empty | #Other)* #DocStringSeparator
DocString_AlternativeSepatator := #DocStringAlternativeSeparator (#Empty | #Other)* #DocStringAlternativeSeparator


Tags! := #TagLine+

Feature_Description := Description_Helper
Background_Description := Description_Helper
Scenario_Description := Description_Helper
ScenarioOutline_Description := Description_Helper
Examples_Description := Description_Helper

// we need to explicitly mention comment, to avoid merging it into the description line's #Other token
Description_Helper := Description? #Comment*
Description! := (#Empty | #Other)+

